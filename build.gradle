import java.nio.charset.Charset
import java.nio.file.Files

plugins {
    id 'java'
    id 'signing'
    id 'checkstyle'
    id("info.solidsoft.pitest") version "1.9.11"
    id("com.github.johnrengelman.shadow") version "7.1.2"
    id("com.github.spotbugs") version "5.1.3"
    id 'maven-publish'
}

defaultTasks 'clean', 'build', 'jar', 'publishToMavenLocal'

def appGroup = 'pl.coffeepower'
def appVersion = Files.readAllLines(projectDir.toPath().resolve('version.txt'), Charset.forName("UTF-8"))
        .first()
        .trim()
def appName = rootProject.name

group = appGroup
version = appVersion.endsWith('SNAPSHOT') ?
        appVersion + '-' + (new Date().format('yyyyMMddHHmmss')) :
        appVersion

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    options.encoding = 'UTF-8'
}

build.dependsOn 'pitest'

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocsJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        GuiceLiquibase(MavenPublication) {
            from components.java
            groupId appGroup
            artifactId appName
            version appVersion
            artifacts = [jar, sourcesJar, javadocsJar]
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'com.google.inject', name: 'guice', version: '5.0.1'
    implementation group: 'org.liquibase', name: 'liquibase-core', version: '4.3.5'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.32'

    testRuntimeOnly group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.32'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testImplementation group: 'org.hsqldb', name: 'hsqldb', version: '2.5.2'
    testImplementation group: 'nl.jqno.equalsverifier', name: 'equalsverifier', version: '3.7.1'
    testImplementation group: 'be.joengenduvel.java.verifiers', name: 'to-string', version: '1.0.2'
    testImplementation 'com.github.spotbugs:spotbugs-annotations:4.7.3'
}



checkstyle {
    toolVersion = '9.3'
    ignoreFailures = false
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = false
        html.required = true
    }
}

pitest {
    threads = 2
}
spotbugs {
    excludeFilter = file('config/spotbugs/exclude.xml')
}
